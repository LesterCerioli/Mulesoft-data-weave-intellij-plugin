import java.time.Instant
import java.time.ZoneOffset
import java.time.format.DateTimeFormatter


plugins {
    id "org.jetbrains.intellij" version "0.3.1"
}

version = pluginVersion


allprojects {

    apply plugin: 'org.jetbrains.intellij'
    apply plugin: 'java'

    if (ijPluginRepoChannel.equalsIgnoreCase("Develop")) {
        version = pluginVersion + "-" + DateTimeFormatter.ofPattern("yyyyMMdd.HHmmss").withZone(ZoneOffset.UTC).format(Instant.now())
    } else {
        version = pluginVersion
    }

    configurations {
        gen
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "http://central.maven.org/maven2/"
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            url "https://repository.mulesoft.org/releases/"
        }
        maven {
            url "http://repository.mulesoft.org/snapshots/"
        }
        maven {
            url "https://repository.mulesoft.org/nexus/content/repositories/releases"
        }
    }

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
    group = 'org.mule.tooling.intellij'

    task jarSources(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    intellij {
        version = "2018.1.3"
        updateSinceUntilBuild = false
        downloadSources = false
        sandboxDirectory = "${rootProject.buildDir}/idea-sandbox"
    }

    publishPlugin {
        username System.getenv('IJ_REPO_USERNAME')
        password System.getenv('IJ_REPO_PASSWORD')
        channels = [ijPluginRepoChannel]
    }

    assemble.dependsOn jarSources

}


