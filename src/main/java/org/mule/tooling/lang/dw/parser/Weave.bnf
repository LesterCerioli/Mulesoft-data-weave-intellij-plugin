{
  parserClass="org.mule.tooling.lang.dw.parser.WeaveParser"
  parserUtilClass="org.mule.tooling.lang.dw.parser.WeaveParserUtil"
  psiImplUtilClass = 'org.mule.tooling.lang.dw.parser.psi.WeavePsiImplUtils'
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
  psiClassPrefix="Weave"
  psiImplClassSuffix="Impl"
  psiPackage="org.mule.tooling.lang.dw.parser.psi"
  psiImplPackage="org.mule.tooling.lang.dw.parser.psi.impl"
  elementTypeHolderClass="org.mule.tooling.lang.dw.parser.psi.WeaveTypes"
  elementTypeClass="org.mule.tooling.lang.dw.parser.WeaveElementType"
  tokenTypeClass="org.mule.tooling.lang.dw.parser.WeaveTokenType"

  tokens = [
        L_PARREN='('
        R_PARREN=')'

        L_CURLY='{'
        R_CURLY='}'
        L_BRACKET='['
        R_BRACKET=']'
        SUB_TYPE='<:'
        COMMA=','
        COLON=':'
        FAT_ARROW='=>'

        LINE_COMMENT="regexp://([^\r\n])*?"

        DOUBLE_QUOTED_STRING="regexp:\"([^\\\"\r\n]|\\[^\r\n])*\"?"
        SINGLE_QUOTED_STRING="regexp:'([^\\\'\r\n]|\\[^\r\n])*'?"
        BACKTIKED_QUOTED_STRING="regexp:`([^\\\'\r\n]|\\[^\r\n])*`?"

        MIME_TYPE_KEYWORD="regexp:[\"text\" | \"audio\" | \"video\" | \"application\" | \"multipart\" | \"image\" ]\"/\"[a-zA-Z\-_]+"

        //
        DOCUMENT_SEPARATOR="---"
        UNDERSCORE="_"
        NOT_EQUAL="!="
        SIMILAR="~="
        TILDE="~"
        EQUAL="=="
        EQ="="
        LESS="<"
        LESS_EQUAL="<="
        GREATER=">"
        GREATER_EQUAL=">="
        AS="as"
        IS="is"
        FROM_KEYWORD="from"
        PLUS="+"
        MINUS="-"
        MULTIPLY="*"
        DIVISION="/"
        MODULO="%"

        AT="@"
        QUESTION="?"
        ESCLAMATION="!"
        HASH="#"

        AND_KEYWORD="and"
        OR_KEYWORD="or"

        ARROW_TOKEN="->"

        IF="if"

        TRUE_LITERAL="true"
        FALSE_LITERAL="false"
        RULE_NULL_LITERAL="null"

        VERSION_DIRECTIVE_KEYWORD="%dw"
        INPUT_DIRECTIVE_KEYWORD="input"
        VAR_DIRECTIVE_KEYWORD="var"
        FUNCTION_DIRECTIVE_KEYWORD="fun"
        NAMESPACE_DIRECTIVE_KEYWORD="ns"
        TYPE_DIRECTIVE_KEYWORD="type"
        MATCHES_KEYWORD="matches"
        MATCH_KEYWORD="match"
        OUTPUT_DIRECTIVE_KEYWORD="output"
        NOT_KEYWORD="not"
        CASE_KEYWORD="case"

        OR="|"
        XOR="^"
        AND="&"
        OPEN_CLOSE_KEYWORD="{|"
        OPEN_CLOSE_ORDERED_KEYWORD="{-|"
        OPEN_ORDERED_KEYWORD="{-"

        CLOSE_ORDERED_KEYWORD="-}"
        CLOSE_CLOSE_ORDERED_KEYWORD="|-}"
        CLOSE_CLOSE_KEYWORD="|}"

        PACKAGE_SEPARATOR="::"
  ]

    extends(".*(Directive)")=Directive
    extends(".*(Expression|Literal)")=Expression
    extends(".*(Type)")=Type
    extends(".*(Pattern)")=Pattern
    pin(".*Directive") =1

}

root ::= Document

Document ::= Header ('---'  Body) ? | Body{
    implements="com.intellij.psi.NavigatablePsiElement"
    methods=[getPresentation]
}

Header::= Directive+ {pin=2}

Directive
         ::= VersionDirective
           | NamespaceDirective
           | VariableDirective
           | OutputDirective
           | InputDirective
           | TypeDirective
           | ImportDirective
           | FunctionDirective {recoverWhile=HeaderRecover}

private HeaderRecover::= !('---'|OUTPUT_DIRECTIVE_KEYWORD|'type'|'fun'|'ns'|'var'|'%dw'|'input'|IMPORT_DIRECTIVE_KEYWORD)

VersionDirective::= '%dw'  DOUBLE_LITERAL

ImportDirective::= IMPORT_DIRECTIVE_KEYWORD (((ImportedElement (',' ImportedElement)*) | '*') 'from')? FqnIdentifier ('as' Identifier)?

ImportedElement::= Identifier ('as' Identifier)?

NamespaceDirective::= 'ns' Identifier NAMESPACE_URI

VariableDirective::= 'var' VariableDefinition

OutputDirective::= OUTPUT_DIRECTIVE_KEYWORD (":" Type)? DataFormat Options?

TypeDirective::= 'type' Identifier TypeParameterDeclaration? '=' (TypeLiteral | UndefinedLiteral)

FunctionDirective::= 'fun' FunctionDefinition

FunctionDefinition::=Identifier TypeParameterDeclaration? L_PARREN ( FunctionParameter ( ',' FunctionParameter )* )? R_PARREN ( ":" Type "=" | "=")? Expression {
     methods = [getName setName getNameIdentifier]
     implements="org.mule.tooling.lang.dw.parser.psi.WeaveNamedElement"
     mixin="org.mule.tooling.lang.dw.parser.psi.WeaveNamedElementImpl"
     pin=3
}

Schema::= '{' ( SchemaElement ( ',' SchemaElement )* )? '}'

SchemaElement::= (Identifier | StringLiteral) ':' LiteralExpression

InputDirective::= 'input' VARIABLE_DECLARATION DataFormat Options?

Options::= OptionElement ( ',' OptionElement )*

OptionElement::= Identifier '=' LiteralExpression

Body ::= Expression

ObjectExpression ::= SingleKeyValuePairObj | MultipleKeyValuePairObj {
    implements="com.intellij.psi.NavigatablePsiElement"
    methods=[getPresentation]
}

SingleKeyValuePairObj ::= KeyValuePair

MultipleKeyValuePairObj::= '{' objectEntryRule? (',' objectEntryRule)* ','? '}' {pin= 1}

private objectEntryRule ::= KeyValuePair | DynamicKeyValuePair

KeyValuePair::= ConditionalKeyValuePair | SimpleKeyValuePair

DynamicKeyValuePair ::= EnclosedExpression

SimpleKeyValuePair ::= Key ':' Expression
{
    implements="com.intellij.psi.NavigatablePsiElement"
    methods=[getPresentation]
}
ConditionalKeyValuePair ::= '(' Key ':' Expression ')' IF SimpleExpression

Key::=  KeyExpression Attributes?

KeyExpression::= DeclaredNamespace? (Identifier | StringLiteral| EnclosedExpression)

DeclaredNamespace::= Identifier '#'

Attributes::= '@(' ( AttributeElement ( ',' AttributeElement )* )? ')'

AttributeElement::= ConditionalAttribute
           | Attribute
           | EnclosedExpression

Attribute::= KeyExpression ':' Expression

ConditionalAttribute::= '(' KeyExpression ':' Expression ')' IF Expression {pin = 6}

Expression::= MatchExpression
            | CustomInterpolatorExpression
            | DefaultValueExpression
            | BinaryExpression
            | SimpleExpression

CustomInterpolatorExpression ::= Identifier BACKTIKED_QUOTED_STRING

private SimpleExpression ::=
             OrExpression
            | AndExpression
            | EqualityExpression
            | GreaterThanExpression
            | AdditionSubtractionExpression
            | RightShiftExpression
            | LeftShiftExpression
            | MultiplicationDivisionExpression
            | AsExpression
            | IsExpression
            | ValueExpression
            | FunctionCallExpression
            | DotSelectorExpression
            | BracketSelectorExpression


private ValueExpression::=UndefinedLiteral
                  | UnaryMinusExpression
                  | NotExpression
                  | ConditionalExpression
                  | UsingExpression
                  | LambdaLiteral
                  | ObjectDeconstructExpression
                  | ObjectExpression
                  | ArrayDeconstructExpression
                  | ArrayExpression
                  | VariableReferenceExpression
                  | LiteralExpression
                  | EnclosedExpression

UnaryMinusExpression ::= '-' SimpleExpression {pin=1}

BinaryExpression ::= Expression Identifier SimpleExpression
    {
        methods=[ getLeft  getRight]
    }

AsExpression
         ::= Expression AS TypeLiteral {pin=2}

DefaultValueExpression
         ::= Expression DEFAULT SimpleExpression {pin=2}

OrExpression
         ::= Expression 'or' AndExpression {pin=2}

AndExpression
         ::= Expression 'and' EqualityExpression {pin=2}

EqualityExpression
         ::= Expression ( '==' | '!=' | '~=' ) GreaterThanExpression {pin=2}

IsExpression
         ::= Expression IS TypeLiteral {pin=2}

NotExpression ::= 'not' ValueExpression

GreaterThanExpression
         ::= Expression  ( '>' | '>=' | '<' | '<=' ) AdditionSubtractionExpression {pin=2}

AdditionSubtractionExpression
         ::= Expression  ( '+' | '-' ) Expression {pin=2}

RightShiftExpression
         ::= Expression '>''>' MultiplicationDivisionExpression {pin=3}

LeftShiftExpression
         ::= Expression '<''<' MultiplicationDivisionExpression {pin=3}

MultiplicationDivisionExpression ::= Expression  ( '*' | '/' ) Expression {pin=2}

FunctionCallExpression ::= Expression '(' ( Expression ( ',' Expression )* )? ')'


UsingExpression ::=USING '(' VariableDefinition ( ',' VariableDefinition )* ')' Expression {pin=1}


ConditionalExpression ::= ( IF | UNLESS ) EnclosedExpression Expression ELSE Expression {pin=1}

EnclosedExpression ::= '(' Expression ')'

//Selectors
DotSelectorExpression ::=Expression ( '..' | '.' ) Selector? ('!'| '?')? {pin=2}

BracketSelectorExpression::=Expression '[' ('?' |'@' | '^' |'*')?  (BinaryExpression | SimpleExpression )  ']' ('!'| '?')? {pin=5}

MatchExpression ::= Expression 'match' (PatternMatcherExpression )

PatternMatcherExpression ::= '{' ('case' Pattern)+ (DefaultPattern)? '}' {pin = 2}

Pattern ::= RegexPattern
          | NamedRegexPattern
          | EmptyArrayPattern
          | EmptyObjectPattern
          | ObjectDeconstructPattern
          | ArrayDeconstructPattern
          | TypePattern
          | NamedTypePattern
          | LiteralPattern
          | NamedLiteralPattern
          | ExpressionPattern

EmptyArrayPattern ::= '['']'  '->' Expression

EmptyObjectPattern ::= '{''}'  '->' Expression

ObjectDeconstructPattern ::= '{' Identifier ':' Identifier '~' Identifier '}' '->' Expression {pin=5}

ArrayDeconstructPattern::= '[' Identifier '~' Identifier ']' '->' Expression {pin=3}

LiteralPattern ::= LiteralExpression '->' Expression

ExpressionPattern ::= Identifier IF SimpleExpression '->' Expression {
     methods = [getName setName getNameIdentifier]
     implements="org.mule.tooling.lang.dw.parser.psi.WeaveNamedElement"
     mixin="org.mule.tooling.lang.dw.parser.psi.WeaveNamedElementImpl"
}

NamedLiteralPattern ::= Identifier ':' LiteralExpression '->' Expression {
     methods = [getName setName getNameIdentifier]
     implements="org.mule.tooling.lang.dw.parser.psi.WeaveNamedElement"
     mixin="org.mule.tooling.lang.dw.parser.psi.WeaveNamedElementImpl"
}

TypePattern ::= 'is' Type '->' Expression

NamedTypePattern ::= Identifier "is" TypeLiteral '->' Expression {
    methods = [getName setName getNameIdentifier]
    implements="org.mule.tooling.lang.dw.parser.psi.WeaveNamedElement"
    mixin="org.mule.tooling.lang.dw.parser.psi.WeaveNamedElementImpl"
}

RegexPattern ::= 'matches' AnyRegexLiteral '->' Expression

NamedRegexPattern ::= Identifier 'matches' AnyRegexLiteral '->' Expression {
    methods = [getName setName getNameIdentifier]
    implements="org.mule.tooling.lang.dw.parser.psi.WeaveNamedElement"
    mixin="org.mule.tooling.lang.dw.parser.psi.WeaveNamedElementImpl"
}

DefaultPattern ::= ELSE '->' Expression {pin=1}

Selector ::= (ValueSelector |
         AttributeSelector |
         NamespaceSelector |
         SchemaSelector |
         MultiValueSelector)

ValueSelector ::= (DeclaredNamespace? (StringLiteral|Identifier))

AttributeSelector ::='@'(DeclaredNamespace? (StringLiteral|Identifier))?

SchemaSelector ::='^'(DeclaredNamespace? (StringLiteral|Identifier))?

NamespaceSelector ::='#'

MultiValueSelector ::= '*'(DeclaredNamespace? (StringLiteral|Identifier))

VariableReferenceExpression ::= FqnIdentifier
//{
//        methods = [getVariableName getReference getName setName getNameIdentifier]
//        implements="org.mule.tooling.lang.dw.parser.psi.WeaveNamedElement"
//        mixin="org.mule.tooling.lang.dw.parser.psi.WeaveNamedElementImpl"
//   }

VariableDefinition ::= VARIABLE_DECLARATION '='  Expression  {
            methods=[getVariableName getVariableValue getName setName getNameIdentifier]
            mixin="org.mule.tooling.lang.dw.parser.psi.WeaveVariableImpl"
            implements="org.mule.tooling.lang.dw.parser.psi.WeaveVariable"
            name="Variable"
        }

private VARIABLE_DECLARATION ::= Identifier (":" Type)?

//Literal Values
LiteralExpression
         ::= BooleanLiteral
           | NullLiteral
           | StringLiteral
           | NumberLiteral
           | AnyDateLiteral
           | AnyRegexLiteral


UndefinedLiteral ::= '?''?''?'

LambdaLiteral ::= TypeParameterDeclaration? '(' ( FunctionParameter ( ',' FunctionParameter )* )? ')' (':' Type)?  '->' SimpleExpression {pin = 6}

private TypeParameterDeclaration ::= '<' (TypeParameter (',' TypeParameter)*)? '>'

FunctionParameter ::= VARIABLE_DECLARATION ('=' Expression)?  {
        methods=[getVariableName getVariableValue getName setName getNameIdentifier]
        mixin="org.mule.tooling.lang.dw.parser.psi.WeaveVariableImpl"
        implements="org.mule.tooling.lang.dw.parser.psi.WeaveVariable"

     }

NullLiteral ::= NULL_LITERAL_KEYWORD

BooleanLiteral ::= TRUE_LITERAL
                        | FALSE_LITERAL

StringLiteral ::= DOUBLE_QUOTED_STRING
                      | BACKTIKED_QUOTED_STRING
                      | SINGLE_QUOTED_STRING { methods = [getValue] }

AnyDateLiteral::= RULE_ANY_DATE

AnyRegexLiteral::= RULE_ANY_REGEX

ArrayDeconstructExpression::= '[' !ConditionalArrayElement Expression '~' Expression ']' {pin=4}

ObjectDeconstructExpression ::= '{' KeyValuePair '~' Expression '}' {pin=3}

ArrayExpression::= '[' ( ArrayElement ( ',' ArrayElement )* (',')? )? ']'
{
    implements="com.intellij.psi.NavigatablePsiElement"
    methods=[getPresentation]
}

private ArrayElement::= ConditionalArrayElement | Expression

private ConditionalArrayElement ::= '(' Expression ')' IF SimpleExpression {pin=4}

private TypeLiteral ::= Type

Type ::= UnionType

private BasicTypeExpression ::= LambdaType | CloseOrderedObjectType | OrderedObjectType | CloseObjectType | ObjectType  | ReferenceType | ('(' Type ')')

TypeParameter ::= Identifier ( '<:' Type)?

UnionType ::= IntersectionType UnionTypeExpression* {pin= 2}

private UnionTypeExpression ::= ('|' IntersectionType) {pin= 2}

IntersectionType ::= BasicTypeExpression (IntersectionTypeExpression)* {pin= 2}

private IntersectionTypeExpression ::=  ('&' BasicTypeExpression) {pin= 2}

ObjectType ::=   '{' ((KeyValuePairType)? (',' KeyValuePairType)*)?  '}' (Schema)?

CloseObjectType ::=  '{|' ((KeyValuePairType)? (',' KeyValuePairType)*)? '|}' (Schema)?

OrderedObjectType ::=   '{-' ((KeyValuePairType)? (',' KeyValuePairType)*)?  '-}' (Schema)?

CloseOrderedObjectType ::=   '{-|' ((KeyValuePairType)? (',' KeyValuePairType)*)?  '|-}' (Schema)?

KeyValuePairType ::=  KeyType ('*')? ('?')? ":" Type

KeyType ::= NameType AttributesType?

AttributesType ::= "@""(" NameType ":" Type (',' NameType ":" Type)* ")" {pin= 1}

NameType ::= ((Identifier"#")?(Identifier | StringLiteral) ('?')?) | '_' | '('TypeParameter')'

LambdaType ::= '(' (LambdaTypeParameter (',' LambdaTypeParameter)*)? ')' '->' Type {pin= 5}

LambdaTypeParameter ::= NamedLambdaTypeParameter | Type

private NamedLambdaTypeParameter ::= Identifier ':' Type {pin=2}

ReferenceType ::= FqnIdentifier ('<' Type (',' Type)* '>')? (Schema)?

DataFormat ::= MIME_TYPE_KEYWORD

NumberLiteral ::= DOUBLE_LITERAL | INTEGER_LITERAL

FqnIdentifier ::= CustomLoader? IdentifierPackage Identifier

IdentifierPackage ::= (Identifier '::')*

CustomLoader ::= Identifier"!"

Identifier ::= (DOLLAR_VARIABLE | ID | 'match' |'matches' | 'from')  {methods = [getName] }
